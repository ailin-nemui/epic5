dnl ----------------------------------------------------------
dnl configure.in for ircii, 2.3.  copyright (c) matthew green, 1993,
dnl except those bits attributed otherwise.
dnl thanks to the screen 3.3 configure.in for giving me examples to
dnl work from and steal ;)
dnl
dnl @(#)$Id: configure.in,v 1.61 2008/01/05 19:00:24 jnelson Exp $

AC_REVISION($Revision: 1.61 $)dnl
AC_INIT(source/irc.c)
AC_CONFIG_HEADER(include/defs.h)

unset GREP_OPTIONS

dnl ----------------------------------------------------------
dnl
dnl grok the version number - from source/irc.c
dnl

VERSION=`sed -n -e 's/";$//' -e '/const char irc_version.. = .EPIC.-/s///p' -e '/const char irc_version/q' < $srcdir/source/irc.c` 
echo this is EPIC5 version $VERSION
echo
AC_SUBST(VERSION)

dnl ----------------------------------------------------------
dnl
dnl initial stuff
dnl

AC_PROG_CC
AC_PROG_CPP
AC_ISC_POSIX
AC_AIX

dnl ----------------------------------------------------------
AC_MSG_CHECKING(for an extra library directory)
AC_ARG_WITH(localdir,
[  --with-localdir=/usr/local An extra directory to look for stuff.],[
   if test "x$withval" = "x" ; then
      with_extra_libdir="no"
   elif test -d "$withval" ; then
      with_extra_libdir="yes"
      LIBS="-L$withval/lib $LIBS"
      CFLAGS="-I$withval/include/ $CFLAGS"
   else
      with_extra_libdir="no"
   fi
],[
   with_extra_libdir="no"
])
AC_MSG_RESULT($with_extra_libdir)

dnl ----------------------------------------------------------
dnl ----------------------------------------------------------
dnl
dnl System specific checks
dnl
dnl ----------------------------------------------------------
dnl ----------------------------------------------------------

dnl
dnl Checking for solaris cc
dnl
AC_MSG_CHECKING(for solaris cc)
( $CC -V 2>&1 | grep 'Sun C' > /dev/null ) && \
	CFLAGS="$CFLAGS -erroff=E_ARG_INCOMPATIBLE_WITH_ARG,E_ASSIGNMENT_TYPE_MISMATCH"
AC_MSG_RESULT(ok)

dnl
dnl checking for svr4's /usr/ccs/lib
dnl
if test -d /usr/ccs/lib; then
	LIBS="-L/usr/ccs/lib $LIBS"
fi


dnl ----------------------------------------------------------
AC_MSG_CHECKING(if you want threaded stdout)
AC_ARG_WITH(threaded-stdout,
[  --with-threaded-stdout[=yes] Threaded stdout so the client doesn't block when gnu screen malfunctions.],[
	if test "x$withval" = "x" ; then
		with_threaded_stdout="no"
	elif test "x$withval" = "xy" ; then
		with_threaded_stdout="yes"
		CFLAGS="$CFLAGS -pthread"
	elif test "x$withval" = "xY" ; then
		with_threaded_stdout="yes"
		CFLAGS="$CFLAGS -pthread"
	elif test "x$withval" = "xyes" ; then
		with_threaded_stdout="yes"
		CFLAGS="$CFLAGS -pthread"
	elif test "x$withval" = "xYES" ; then
		with_threaded_stdout="yes"
		CFLAGS="$CFLAGS -pthread"
	else
		with_threaded_stdout="no"
	fi
],[
	with_threaded_stdout="no"
])
AC_MSG_RESULT($with_threaded_stdout)
if test "x$with_threaded_stdout" = "xyes" ; then
	AC_DEFINE(WITH_THREADED_STDOUT)
	threading=1
fi


dnl ----------------------------------------------------------
dnl   Where does this belong?
AC_MSG_CHECKING(which multiplexer function to use)
AC_ARG_WITH(multiplex,
[  --with-multiplex[=TYPE] Multiplexer type (select,poll,freebsd-kqueue,pthread,solaris-ports)],[
	if test "x$withval" = "x"; then
		with_multiplex="select"
	elif test "x$withval" = "xselect"; then
		with_multiplex="select"
	elif test "x$withval" = "xpoll"; then
		with_multiplex="poll"
	elif test "x$withval" = "xfreebsd-kqueue"; then
		with_multiplex="kqueue"
	elif test "x$withval" = "xsolaris-ports"; then
		with_multiplex="port_create"
	elif test "x$withval" = "xpthread"; then
		with_multiplex="pthread_create"
		CFLAGS="$CFLAGS -pthread"
	else
		with_multiplex="select"
	fi
],[
	with_multiplex="select"
])
AC_MSG_RESULT($with_multiplex)
AC_CHECK_FUNC($with_multiplex, [
	if test "x$with_multiplex" = "xselect" ; then
		AC_DEFINE(USE_SELECT)
		threading=0
	elif test "x$with_multiplex" = "xpoll" ; then
		AC_DEFINE(USE_POLL)
		threading=0
	elif test "x$with_multiplex" = "xkqueue" ; then
		AC_DEFINE(USE_FREEBSD_KQUEUE)
		threading=0
	elif test "x$with_multiplex" = "xport_create" ; then
		AC_DEFINE(USE_SOLARIS_PORTS)
		threading=0
	elif test "x$with_multiplex" = "xpthread_create" ; then
		AC_DEFINE(USE_PTHREAD)
		threading=1
	fi
],[
	AC_DEFINE(USE_SELECT)
	threading=0
])


dnl ----------------------------------------------------------
dnl
dnl Check for libraries before we check for functions!
dnl

if test -z "$libsocket"; then
	AC_CHECK_LIB(socket, socket, LIBS="$LIBS -lsocket",)
fi

AC_CHECK_LIB(archive, archive_read_new, 
[LIBS="$LIBS -larchive"
 AC_DEFINE(HAVE_LIBARCHIVE)],)

AC_ARG_WITH(ssl,
[  --with-ssl[=PATH]       Include SSL support (DIR is OpenSSL's install dir).],[
	if test -z "$withval"; then
		with_ssl="yes"
	fi
])

AC_MSG_CHECKING(whether to include SSL support)
if test "x$with_ssl" = "xno"; then
	AC_MSG_RESULT(no)
else
	saved_LIBS="$LIBS"
	saved_CFLAGS="$CFLAGS"

	# First just try to see if it exists in the default location
	LIBS="$saved_LIBS -lssl -lcrypto"
	CFLAGS="$saved_CFLAGS"
	AC_TRY_LINK([#include <openssl/ssl.h>],
		[SSL_accept(0);], have_ssl="yes")

	# Otherwise, go hunting for it.
	if test -z "$have_ssl" ; then
	    for ssldir in "$with_ssl" /usr/local/ssl /usr/local/openssl; do
		if test -n "$have_ssl"; then
			break;
		fi

		if test -z "$ssldir"; then
			continue;
		fi

		if test -f "$ssldir/include/openssl/ssl.h" &&
			test -f "$ssldir/lib/libssl.a"; then
		    CFLAGS="$saved_CFLAGS -I$ssldir/include"
		    LIBS="$saved_LIBS -L$ssldir/lib -lssl -lcrypto"
		    AC_TRY_LINK([#include <openssl/ssl.h>],
				[SSL_accept(0);], have_ssl="yes")
		fi
	    done
	fi

	# If we found it somewhere, great!  Otherwise, revert.
	if test ! -n "$have_ssl"; then
		LIBS="$saved_LIBS"
		CFLAGS="$saved_CFLAGS"
		AC_MSG_RESULT(no)
		if test "x$with_ssl" != "x"; then
			AC_MSG_ERROR([You requested SSL support, but OpenSSL was not found. Please supply a pathname to OpenSSL])
		fi
	else
	    if test "x$threading" = "x1" ; then
		AC_MSG_RESULT(yes)
		AC_MSG_CHECKING(whether your SSL is threading compatable)
		AC_TRY_RUN([
#define OPENSSL_THREAD_DEFINES
#include <openssl/opensslconf.h>
main ()
{
#if defined(THREADS)
        exit(0);
#else
        exit(1);
#endif
}
],
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_SSL),
		AC_MSG_RESULT(no. Your system can not do SSL with pthreads), :)
	    else
		AC_DEFINE(HAVE_SSL)
		AC_MSG_RESULT(yes)
	    fi
	fi
fi


dnl this is for isc.  need the nsl_s library as well.
if test -z "$libinet"; then
	AC_CHECK_LIB(inet, socket, libnsl=1; LIBS="$LIBS -linet -lnsl_s",)
fi

if test -z "$libnsl"; then
	AC_CHECK_LIB(nsl, gethostname, LIBS="$LIBS -lnsl",)
fi

AC_CHECK_LIB(sun, getpwnam, LIBS="$LIBS -lsun",)
AC_CHECK_LIB(dgc, inet_addr, LIBS="$LIBS -ldgc",)
AC_CHECK_LIB(resolv, gethostbyname, LIBS="$LIBS -lresolv",)
AC_CHECK_LIB(crypt, crypt, LIBS="$LIBS -lcrypt",)
AC_CHECK_LIB(m, pow, LIBS="$LIBS -lm",)
AC_CHECK_LIB(rt, clock_gettime, LIBS="$LIBS -lrt",)

termcap=0;
warn_again=0;
AC_MSG_CHECKING(whether you want to use termcap)
AC_ARG_WITH(termcap,
[  --with-termcap          Forcibly refuse to use terminfo/ncurses ],
[   AC_MSG_RESULT(yes)
    AC_CHECK_LIB(termcap, tgetent, termcap=1;LIBS="-ltermcap $LIBS",
      AC_CHECK_LIB(termlib, tgetent, termcap=1;LIBS="-ltermlib $LIBS",
        AC_CHECK_LIB(curses,  tgetent, termcap=1;LIBS="-lcurses $LIBS",
          AC_MSG_WARN(I cant find tgetent.  Youll have to help me.))))],
[   AC_MSG_RESULT(no)
    AC_CHECK_LIB(ncurses, setupterm, LIBS="-lncurses $LIBS",
      AC_CHECK_LIB(tinfo,   setupterm, LIBS="-ltinfo $LIBS",
	  AC_CHECK_LIB(curses, setupterm, LIBS="-lcurses $LIBS",
	    AC_CHECK_LIB(curses, tgetent, LIBS="-lcurses $LIBS",
              AC_CHECK_LIB(termlib, tgetent, LIBS="-ltermlib $LIBS",
                AC_CHECK_LIB(termcap, tgetent, LIBS="-ltermcap $LIBS",
	          AC_MSG_WARN(I cant find tgetent.  Youll have to help me.)
		  warn_again=1))))))])

dnl ----------------------------------------------------------
dnl
dnl Checking for headers, functions, and a type declarations
dnl

AC_CHECK_HEADERS(fcntl.h inttypes.h math.h ndbm.h netdb.h regex.h stddef.h stdint.h sys/fcntl.h sys/file.h sys/filio.h sys/select.h sys/sysctl.h sys/syslimits.h sys/time.h sys/un.h termcap.h,)

AC_CHECK_FUNC(arc4random, AC_DEFINE(HAVE_ARC4RANDOM),)
AC_CHECK_FUNC(clock_gettime, AC_DEFINE(HAVE_CLOCK_GETTIME),)
AC_CHECK_FUNC(fchdir, AC_DEFINE(HAVE_FCHDIR),)
AC_CHECK_FUNC(fpathconf, AC_DEFINE(HAVE_FPATHCONF),)
AC_CHECK_FUNC(getlogin, AC_DEFINE(HAVE_GETLOGIN),)
AC_CHECK_FUNC(getaddrinfo, AC_DEFINE(HAVE_GETADDRINFO),)
AC_CHECK_FUNC(getnameinfo, AC_DEFINE(HAVE_GETNAMEINFO),)
AC_CHECK_FUNC(getpass, AC_DEFINE(HAVE_GETPASS),)
AC_CHECK_FUNC(getpgid, AC_DEFINE(HAVE_GETPGID),)
AC_CHECK_FUNC(getpgrp, AC_FUNC_GETPGRP,)
AC_CHECK_FUNC(getservbyport, AC_DEFINE(HAVE_GETSERVBYPORT),)
AC_CHECK_FUNC(gettimeofday, AC_DEFINE(HAVE_GETTIMEOFDAY),)
AC_CHECK_FUNC(killpg, AC_DEFINE(HAVE_KILLPG),)
AC_CHECK_FUNC(memmove, AC_DEFINE(HAVE_MEMMOVE),)
AC_CHECK_FUNC(nanosleep, AC_DEFINE(HAVE_NANOSLEEP),)
AC_CHECK_FUNC(uname, AC_DEFINE(HAVE_UNAME),)
AC_CHECK_FUNC(realpath, AC_DEFINE(HAVE_REALPATH),)
AC_CHECK_FUNC(setenv, AC_DEFINE(HAVE_SETENV),)
AC_CHECK_FUNC(setsid, AC_DEFINE(HAVE_SETSID),)
if test $termcap -eq 0 ; then
	AC_CHECK_FUNC(setupterm, AC_DEFINE(HAVE_TERMINFO),)
fi
AC_CHECK_FUNC(snprintf, AC_DEFINE(HAVE_SNPRINTF),)
AC_CHECK_FUNC(strerror, AC_DEFINE(HAVE_STRERROR),)
AC_CHECK_FUNC(strlcat, AC_DEFINE(HAVE_STRLCAT),)
AC_CHECK_FUNC(strlcpy, AC_DEFINE(HAVE_STRLCPY),)
AC_CHECK_FUNC(strtoul, AC_DEFINE(HAVE_STRTOUL),)
AC_CHECK_FUNC(sysconf, AC_DEFINE(HAVE_SYSCONF),)
AC_CHECK_FUNC(sysctlbyname, AC_DEFINE(HAVE_SYSCTLBYNAME),)
AC_CHECK_FUNC(tparm, AC_DEFINE(HAVE_TPARM),)
AC_CHECK_FUNC(unsetenv, AC_DEFINE(HAVE_UNSETENV),)
AC_CHECK_FUNC(vsnprintf, AC_DEFINE(HAVE_VSNPRINTF),)
AC_CHECK_FUNC(finite, AC_DEFINE(HAVE_FINITE),)
AC_MSG_CHECKING(for isfinite macro in math.h)
AC_EGREP_HEADER(isfinite, math.h, 
	AC_DEFINE(HAVE_ISFINITE) AC_MSG_RESULT(yes),
	AC_MSG_RESULT(no))

AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_TYPE_SIGNAL
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_FUNC_ALLOCA

dnl ----------------------------------------------------------
dnl Check to see if we can find intptr_t.
dnl
have_intptr_t=""
AC_MSG_CHECKING(for intptr_t in inttypes.h or stdint.h)
AC_EGREP_HEADER(intptr_t, inttypes.h, AC_DEFINE(HAVE_INTPTR_T)
					have_intptr_t=yes)
AC_EGREP_HEADER(intptr_t, stdint.h, AC_DEFINE(HAVE_INTPTR_T)
					have_intptr_t=yes)
if test "x$have_intptr_t" = "x"; then
	AC_MSG_RESULT(no)
else
	AC_MSG_RESULT(yes)
fi

dnl ----------------------------------------------------------
dnl Check to see if we have socklen_t
dnl
AC_MSG_CHECKING(for socklen_t)
AC_EGREP_HEADER(socklen_t, sys/socket.h,
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_SOCKLEN_T),
  AC_MSG_RESULT(no))

dnl ----------------------------------------------------------
dnl
dnl Check to see if we have to include <sys/select.h>
dnl 
dnl
AC_MSG_CHECKING(whether sys/types.h defines fd_set)
AC_EGREP_HEADER(fd_set, sys/types.h,
  AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no)
  AC_MSG_CHECKING(whether sys/select.h defines fd_set)
  AC_EGREP_HEADER(fd_set, sys/select.h,
    AC_MSG_RESULT(yes)
    AC_DEFINE(NEED_SYS_SELECT_H),
    AC_MSG_RESULT(no. ugh.))) 


dnl ----------------------------------------------------------
dnl
dnl check for struct linger
dnl

AC_MSG_CHECKING(for struct linger)
AC_EGREP_HEADER([struct( |	)*linger], sys/socket.h, 
  AC_MSG_RESULT(yes), 
  AC_DEFINE(NO_STRUCT_LINGER) 
  AC_MSG_RESULT(no. ugh.))

dnl ---------------------------------------------------------
dnl
dnl Check for a useful SO_SNDLOWAT.
dnl Some systems (eg, linux) can be bothered to #define SO_SNDLOWAT
dnl but can't be bothered to implement it, instead returning an error.
dnl How completely bogus.  Don't #define it if you don't support it!
dnl
AC_MSG_CHECKING(for useful SO_SNDLOWAT)
AC_TRY_RUN([
#include <sys/types.h>
#include <sys/socket.h>
main ()
{
#ifndef SO_SNDLOWAT
        exit(1);
#else
        int d = socket(AF_INET, SOCK_STREAM, 0);
        int size = 256;
        if (setsockopt(d, SOL_SOCKET, SO_SNDLOWAT, &size, sizeof(size)) < 0)
                exit(1);
        exit (0);
#endif
}
],
  AC_MSG_RESULT(yes, your SO_SNDLOWAT is useful)
  AC_DEFINE(HAVE_SO_SNDLOWAT),
  AC_MSG_RESULT(no, your SO_SNDLOWAT is broken and/or worthless), :)

dnl ----------------------------------------------------------
dnl
dnl Check for a realpath() that works.
dnl
AC_MSG_CHECKING(for useful realpath)
AC_TRY_RUN([
#include <sys/param.h>
#include <stdlib.h>
main ()
{
	char resolved_path[MAXPATHLEN];

	if (realpath("/etc/filethatdoesnotexist", resolved_path) == 0)
		exit(1);
	exit(0);
}
],
  AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no, your realpath is broken and/or worthless)
  AC_DEFINE(HAVE_BROKEN_REALPATH), :)

dnl ----------------------------------------------------------
dnl
dnl Check for sys_siglist declaration
dnl

AC_MSG_CHECKING(for sys_siglist declaration)
AC_TRY_RUN([
#include <sys/types.h>
#include <sys/signal.h>
#include <signal.h>
#include <unistd.h>
main()
{
	char *s = sys_siglist[0];
	exit(0);
}
],
  AC_MSG_RESULT(yes) 
  sys_siglist="1"
  AC_DEFINE(SYS_SIGLIST_DECLARED), 
  AC_MSG_RESULT(no), :)

dnl ----------------------------------------------------------
dnl
dnl Generate signal list, from perl4
dnl
if test x$sys_siglist != x1
then
echo "generating a list of signal names..."
set X `cat /usr/include/signal.h /usr/include/sys/signal.h /usr/include/linux/signal.h 2>&1 | sed 's/^#[	]*/#/' | awk '
$1 ~ /^#define$/ && $2 ~ /^SIG[A-Z0-9]*$/ && $3 ~ /^[1-9][0-9]*$/ {
  [sig[$3] = substr($2,4,20)]
  if (max < $3 && $3 < 60) {
	max = $3
  }
}

END {
  for (i=1; i<=max; i++) {
    [if (sig[i] == "")]
      printf "%d", i
    else
      [printf "%s", sig[i]]
    if (i < max)
      printf " "
  }
  printf "\n"
}
'`
shift
case $# in
0)
  if test -f /bin/csh; then
     set X `/bin/csh -cf 'kill -l'`
     shift
     case $* in
     0) 
       set HUP INT QUIT ILL TRAP ABRT EMT FPE KILL BUS SEGV SYS PIPE ALRM TERM
       ;;
    esac
  fi
  ;;
esac

sig_name="ZERO $*"
if test -f source/sig.inc; then
  /bin/rm -f source/sig.inc
fi
[echo $sig_name | sed -e 's/$/"};/' -e 's/ /", "/g' -e 's/^/static char *sys_siglist[] = { "/' >> source/sig.inc]
fi

dnl ----------------------------------------------------------
dnl
dnl Check for 'intmax_t'
dnl
have_intmax_t=""
AC_MSG_CHECKING(for intmax_t in inttypes.h or stdint.h)
AC_EGREP_HEADER(intmax_t, inttypes.h, have_intmax_t=yes)
AC_EGREP_HEADER(intmax_t, stdint.h, have_intmax_t=yes)

if test "x$have_intmax_t" != "x"; then
   AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE_INTMAX_NATIVE)
   AC_CHECK_FUNC(strtoimax, AC_DEFINE(HAVE_STRTOIMAX),)
else
   AC_MSG_RESULT(no)
   AC_CHECK_SIZEOF(long long, 0)
   if test "$ac_cv_sizeof_long_long" != 0; then
      AC_DEFINE(HAVE_INTMAX_LONG_LONG)
      AC_CHECK_FUNC(strtoll, AC_DEFINE(HAVE_STRTOLL),)
   else
      AC_CHECK_SIZEOF(quad_t, 0)
      if test "$ac_cv_sizeof_quad_t" != 0; then
         AC_DEFINE(HAVE_INTMAX_QUADT)
         AC_CHECK_FUNC(strtoq, AC_DEFINE(HAVE_STRTOQ),)
      else
         AC_DEFINE(HAVE_INTMAX_LONG)
      fi
   fi
fi

dnl ----------------------------------------------------------
dnl
dnl need this for dcc stuff
dnl

AC_CHECK_SIZEOF(unsigned int, 4)
if test "$ac_cv_sizeof_unsigned_int" = 4; then
	AC_DEFINE(UNSIGNED_INT32)
else
	AC_CHECK_SIZEOF(unsigned long, 4)
	if test "$ac_cv_sizeof_unsigned_long" = 4; then
		AC_DEFINE(UNSIGNED_LONG32)
	else
		AC_DEFINE(UNKNOWN_32INT)
	fi
fi

dnl ----------------------------------------------------------
dnl
dnl It makes a big difference if your system's (struct sockaddr)
dnl structures look like this:
dnl
dnl struct sockaddr {
dnl     u_char     sa_len;
dnl     u_char     sa_family;
dnl     char       sa_data[14];
dnl };
dnl
dnl 	or this
dnl
dnl struct sockaddr {
dnl     u_short    sa_family;
dnl     char       sa_data[14];
dnl };
dnl
dnl so check for that here.
dnl
dnl -----------------------------------------------------------

AC_MSG_CHECKING(to see if your struct sockaddr includes a length field)
AC_TRY_RUN([
#include <sys/types.h>
#include <sys/socket.h>
main()
{
	struct sockaddr sa;
	sa.sa_len = 0;
	exit(0);
}
],
  AC_DEFINE(HAVE_SA_LEN)
  AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no), :)

dnl ----------------------------------------------------------
dnl
dnl checks for ipv6-related things
dnl

AC_MSG_CHECKING(whether to support ipv6)
AC_ARG_WITH(ipv6,
[  --with-ipv6             Include IPv6 support],[
	if test "x$withval" = "xno" ; then
		AC_MSG_RESULT(no)
		AC_DEFINE(DO_NOT_USE_IPV6)
	fi
],[AC_MSG_RESULT(yes)])

AC_MSG_CHECKING(for struct sockaddr_storage)
AC_EGREP_HEADER([struct( |	)*sockaddr_storage], sys/socket.h, 
  AC_MSG_RESULT(yes) 
  AC_DEFINE(HAVE_STRUCT_SOCKADDR_STORAGE),
  AC_MSG_RESULT(no))

AC_MSG_CHECKING(for struct sockaddr_in6)
AC_EGREP_HEADER([struct( |	)*sockaddr_in6], netinet/in.h,
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_STRUCT_SOCKADDR_IN6),
  AC_MSG_RESULT(no))

AC_MSG_CHECKING(for struct addrinfo)
AC_EGREP_HEADER([struct( |	)*addrinfo], netdb.h,
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_STRUCT_ADDRINFO),
  AC_MSG_RESULT(no))

dnl ----------------------------------------------------------
dnl
dnl Some implementations of getaddrinfo (*cough*KAME*cough*) don't 
dnl support the AF_UNIX protocol family.  For these partially incomplete 
dnl implementations, we must add our own support in a wrapper.
dnl

AC_MSG_CHECKING(to see if your getaddrinfo supports AF_UNIX)
AC_TRY_RUN([
#include <sys/types.h>
#include <sys/socket.h>
#include <netdb.h>
#include <sys/un.h>
#include <stdlib.h>

main()
{
	struct addrinfo hints;
	struct addrinfo *results;
	int	retval;

	memset(&hints, 0, sizeof(hints));
	hints.ai_flags = 0;
	hints.ai_family = AF_UNIX;
	hints.ai_socktype = SOCK_STREAM;
	hints.ai_protocol = 0;

	retval = getaddrinfo(0, "/tmp/.ircd/6667", &hints, &results);
	if (retval != 0)
		exit(1);
	else
		exit(0);
}
],
  AC_MSG_RESULT(yes),
  AC_DEFINE(GETADDRINFO_DOES_NOT_DO_AF_UNIX)
  AC_MSG_RESULT(no. ugh), :)

dnl ----------------------------------------------------------
dnl
dnl Socks4 or Socks5 or neither?
dnl

AC_MSG_CHECKING(whether to support SOCKS)
AC_ARG_WITH(socks,
[  --with-socks[=PATH]     Compile with SOCKS firewall traversal support.],
[ case "$withval" in
  no)
	AC_MSG_RESULT(no)
	;;
  *)
	if test "x$withval" != "xyes"; then
		LIBS="$LIBS -L$withval"
	fi

	AC_MSG_RESULT(yes)
	LIBS="$LIBS -lsocks"
	AC_DEFINE(SOCKS)
	AC_DEFINE(connect, Rconnect)
	AC_DEFINE(getsockname, Rgetsockname)
	AC_DEFINE(bind, Rbind)
	AC_DEFINE(accept, Raccept)
	AC_DEFINE(listen, Rlisten)
	AC_DEFINE(select, Rselect)
	;;
  esac ],
  AC_MSG_RESULT(no)
)

AC_MSG_CHECKING(whether to support SOCKS5)
AC_ARG_WITH(socks5,
[  --with-socks5[=PATH]    Compile with SOCKS5 firewall traversal support.],
[ case "$withval" in
  no)
	AC_MSG_RESULT(no)
	;;
  *)
	if test "x$withval" != "xyes"; then
		LIBS="$LIBS -L$withval"
		CFLAGS="$CFLAGS -I$withval/../include/"
	fi

	AC_MSG_RESULT(yes)
	LIBS="$LIBS -lsocks5"
	AC_DEFINE(SOCKS)
	AC_DEFINE(USE_SOCKS5)
	;;
  esac ],
  AC_MSG_RESULT(no)
)


dnl ----------------------------------------------------------
dnl
dnl Iconv support?
dnl
AC_ARG_WITH(iconv,
[  --with-iconv[=PATH]       Include iconv support (DIR is eg /usr/local).],[
	if test -z "$withval"; then
		with_iconv="yes"
	fi
])

AC_MSG_CHECKING(whether to include iconv support)
if test "x$with_iconv" = "xno"; then
	AC_MSG_RESULT(no)
else
	saved_LIBS="$LIBS"
	saved_CFLAGS="$CFLAGS"

	# First just try to see if it exists in the default location
	LIBS="$saved_LIBS -liconv"
	CFLAGS="$saved_CFLAGS"
	AC_TRY_LINK([#include <iconv.h>],
		[iconv_open(0, 0);], have_iconv="yes")

	# Otherwise, go hunting for it.
	if test -z "$have_iconv" ; then
	    for iconvdir in "$with_iconv" "$prefix" /usr/local /opt /usr/opt; do
		if test -n "$have_iconv"; then
			break;
		fi

		if test -z "$iconvdir"; then
			continue;
		fi

		if test -f "$iconvdir/include/iconv.h" &&
			test -f "$iconvdir/lib/libiconv.a"; then
		    CFLAGS="$saved_CFLAGS -I$iconvdir/include"
		    LIBS="$saved_LIBS -L$iconvdir/lib -liconv"
		    AC_TRY_LINK([#include <iconv.h>],
				[iconv_open(0, 0);], have_iconv="yes")
		fi
	    done
	fi

	# If we found it somewhere, great!  Otherwise, revert.
	if test ! -n "$have_iconv"; then
		LIBS="$saved_LIBS"
		CFLAGS="$saved_CFLAGS"
		AC_MSG_RESULT(looked but did not find it)
		if test "x$with_iconv" != "x"; then
			AC_MSG_ERROR([You requested iconv support, but iconv was not found. Please supply a pathname to iconv])
		fi
	else
		AC_MSG_RESULT(yes)
	fi

	AC_CHECK_FUNC(iconv_open, AC_DEFINE(HAVE_ICONV),)
fi

dnl ----------------------------------------------------------
dnl
dnl Perl support?
dnl
AC_ARG_WITH(perl,
[  --with-perl[=PATH_TO_PERL_EXE]       Compile with perl support.],
[ 
	perlexe=$withval
],
	perlexe=yes
)

if test "x$perlexe" = "xyes"; then
	perlexe=perl
fi

if test "x$perlexe" = "xno"; then
	AC_MSG_CHECKING(whether to support Perl)
	AC_MSG_RESULT(no)
else
	AC_CHECK_PROG(PERLPROG, $perlexe, "yes", "no")
	AC_MSG_CHECKING(whether to support Perl)
	if test $PERLPROG != "yes" ; then 
		AC_MSG_RESULT(no)
	else
		AC_MSG_RESULT(yes)
		PERLCFLAGS=`$perlexe -MExtUtils::Embed -e ccopts`
		PERLLDFLAGS=`$perlexe -MExtUtils::Embed -e ldopts`
		PERLDOTOH="perl.o"

		AC_DEFINE(HAVE_PERL)
		AC_DEFINE(PERLCFLAGS)
		AC_DEFINE(PERLLDFLAGS)
	fi
fi

dnl ----------------------------------------------------------
dnl
dnl Tcl support?
dnl
AC_ARG_WITH(tcl,
[  --with-tcl[=PATH_TO_tclConfig.sh]      Compile with tcl support.],
[
	tclconfig=$withval
],
	tclconfig="yes"
)

if test "x$tclconfig" = "xyes" ; then
	for i in /usr/lib /usr/local/lib /usr/local/lib/tcl8.4 ; do
		if test -r $i/tclConfig.sh ; then
			tclconfig=$i/tclConfig.sh
			break;
		fi
	done
fi

AC_MSG_CHECKING(whether to support TCL)
if test "x$tclconfig" = "xno"; then
	AC_MSG_RESULT(no)
else
	if test ! -r $tclconfig ; then
		AC_MSG_RESULT(no)
	else
		AC_MSG_RESULT(yes)
		TCLCFLAGS=`(. $tclconfig && echo $TCL_INCLUDE_SPEC)`
		TCLLDFLAGS1=`(. $tclconfig && echo $TCL_LIB_SPEC)`
		TCLLDFLAGS2=`(. $tclconfig && echo $TCL_LIBS)`
		TCLLDFLAGS="$TCLLDFLAGS1 $TCLLDFLAGS2"
		TCLDOTOH="tcl.o"

		AC_DEFINE(HAVE_TCL)
		AC_DEFINE(TCLCFLAGS)
		AC_DEFINE(TCLLDFLAGS)
	fi
fi

dnl ----------------------------------------------------------
dnl
dnl Ruby support?
dnl
AC_ARG_WITH(ruby,
[  --with-ruby[=PATH_TO_RUBY_EXE]       Compile with ruby support.],
[ 
	rubyexe=$withval
],
	rubyexe=yes
)

if test "x$rubyexe" = "xyes"; then
	rubyexe=ruby
fi

if test "x$rubyexe" = "xno"; then
	AC_MSG_CHECKING(whether to support Ruby)
	AC_MSG_RESULT(no)
else
	AC_CHECK_PROG(RUBYPROG, $rubyexe, "yes", "no")
	AC_MSG_CHECKING(whether to support Ruby)
	if test $RUBYPROG != "yes" ; then 
	    AC_MSG_RESULT(no)
	else
	    AC_MSG_RESULT(yes)

	    incdir=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["archdir"]]'`
	    rubylibs=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["LIBRUBYARG"]]'`
	    extralibs=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["LIBS"]]'`
	    RUBYCFLAGS="-I$incdir"
	    RUBYLDFLAGS="$rubylibs $extralibs"
	    RUBYDOTOH="ruby.o"

	    AC_DEFINE(HAVE_RUBY)
	    AC_DEFINE(RUBYCFLAGS)
	    AC_DEFINE(RUBYLDFLAGS)
	fi
fi

dnl ----------------------------------------------------------
dnl ----------------------------------------------------------
dnl
dnl closing stuff
dnl
dnl ----------------------------------------------------------
dnl ----------------------------------------------------------


dnl ----------------------------------------------------------
dnl 
dnl set some last minute sanity defaults
dnl

if test -z "$CFLAGS"; then CFLAGS=-g -O; fi
if test -z "$LDFLAGS"; then LDFLAGS= ; fi
if test -z "$PERLDOTOH"; then PERLDOTOH= ; fi
if test -z "$TCLDOTOH"; then TCLDOTOH= ; fi
if test -z "$RUBYDOTOH"; then RUBYDOTOH= ; fi
if test -z "$bindir"; then bindir=\${prefix}/bin; fi
if test -z "$libdir"; then libdir=\${prefix}/lib; fi
if test -z "$irclibdir"; then irclibdir=\${libdir}/irc; fi
if test -z "$libexecdir"; then libexecdir=\${prefix}/libexec; fi

epic5=`echo "epic5" | sed -e "$program_transform_name"`
if test "$program_prefix" = "NONE" ; then
	program_prefix=
fi

AC_MSG_CHECKING(whether to support bounds checking gcc)
AC_ARG_WITH(bc,
[  --with-bc[=PATH_TO_GCC] You have bounds checking gcc and want to use it.],
[   case "$withval" in
   no)
        AC_MSG_RESULT(no);
        ;;
   *)
        if test "x$withval" != "xyes" ; then
                CC="$withval"
        fi

        CFLAGS="$CFLAGS -Wall -fbounds-checking"
        LDFLAGS="$LDFLAGS -static"
        AC_MSG_RESULT(yes)
        ;;
   esac ],
   AC_MSG_RESULT(no)
)

AC_MSG_CHECKING(whether to turn on FreeBSD WARNS-like compiler warnings)
AC_ARG_WITH(warns,
[  --with-warns            You wish to use FreeBSD WARNS-like compiler warnings.],
[   case "$withval" in
   no)
        AC_MSG_RESULT(no);
        ;;
   *)
        if test "x$withval" != "xyes" ; then
                CFLAGS="$CFLAGS $withval"
        fi

        CFLAGS="$CFLAGS -W -Wall -Wstrict-prototypes -Wmissing-prototypes -Wuninitialized -Wpointer-arith -Wreturn-type -Wcast-qual -Wwrite-strings -Wswitch -Wshadow -Wformat-extra-args -Wno-unused -ansi -fno-builtin"
        AC_MSG_RESULT(yes)
        ;;
   esac ],
   AC_MSG_RESULT(no)
)

AC_MSG_CHECKING(whether your compiler supports -Wno-pointer-sign)
old_cflags="$CFLAGS"
CFLAGS="$CFLAGS -Wno-pointer-sign"
AC_TRY_COMPILE(, [exit(0);], 
	AC_MSG_RESULT(yes),
        AC_MSG_RESULT(no)
	CFLAGS="$old_cflags"
)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(RUBYDOTOH)
AC_SUBST(RUBYCFLAGS)
AC_SUBST(RUBYLDFLAGS)
AC_SUBST(TCLDOTOH)
AC_SUBST(TCLCFLAGS)
AC_SUBST(TCLLDFLAGS)
AC_SUBST(PERLDOTOH)
AC_SUBST(PERLCFLAGS)
AC_SUBST(PERLLDFLAGS)
AC_SUBST(bindir)
AC_SUBST(irclibdir)
AC_SUBST(libexecdir)
AC_SUBST(srcdir)
AC_SUBST(includedir)
AC_SUBST(epic5)
AC_SUBST(program_transform_name)
AC_SUBST(program_prefix)
AC_SUBST(mandir)

AC_OUTPUT(Makefile source/Makefile source/info.c.sh)


dnl ----------------------------------------------------------
dnl
dnl Commencement
dnl

echo
echo "There you go.  Look at include/config.h for any options that "
echo "you might want to change before you 'make'.  Don't forget to "
echo "do a 'make install' before you're done."
echo 

if test "x$warn_again" != "x0" ; then
   echo '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'
   echo '@@@              IMPORTANT WARNING!                 @@@'
   echo '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'
   echo ''
   echo 'I was unable to find tgetent() which means the build will fail.'
   echo 'If you are using linux, you _MUST_ install the ncurses-dev'
   echo 'or ncurses-devel package and run configure again.'
   echo ''
   echo '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'
   echo '@@@              IMPORTANT WARNING!                 @@@'
   echo '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'
fi

